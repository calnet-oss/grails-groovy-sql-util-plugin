//
// This is a Gradle multi-project build.
// See settings.gradle for the subprojects.
//
// The two subprojects are:
// groovy-sql-util-plugin - The main plugin.
// groovy-sql-util-test - Tests using the plugin.
//
// Gradle Multi-Project documentation is here:
// https://docs.gradle.org/current/userguide/multi_project_builds.html
//
buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:6.0.4"
    }
}

allprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    group = 'edu.berkeley.calnet.grails.plugins'
    version = '1.3.0-SNAPSHOT'
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        compile "org.codehaus.groovy:groovy-all:2.4.7"
    }
}
