//
// This is a Gradle multi-project build.
// See settings.gradle for the subprojects.
//
// The two subprojects are:
// groovy-sql-util-plugin - The main plugin.
// groovy-sql-util-test - Tests using the plugin.
//
// Gradle Multi-Project documentation is here:
// https://docs.gradle.org/current/userguide/multi_project_builds.html
//

version projectVersion
group projectGroup

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for defaultCentral_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("defaultCentral_proxy_url")) {
            maven {
                url project.property("defaultCentral_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        else {
            jcenter()
        }
        // grails-core
        if (project.hasProperty("grailsCore_proxy_url")) {
            maven {
                url project.property("grailsCore_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        else {
            maven { url "https://repo.grails.org/grails/core" }
        }
    }
}

def pdep(String depstr) {
    return (depstr.startsWith(":") ? project(depstr) : depstr)
}
