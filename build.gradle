//
// This is a Gradle multi-project build.
// See settings.gradle for the subprojects.
//
// The two subprojects are:
// groovy-sql-util-plugin - The main plugin.
// groovy-sql-util-test - Tests using the plugin.
//
// Gradle Multi-Project documentation is here:
// https://docs.gradle.org/current/userguide/multi_project_builds.html
//
buildscript {
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
        mavenCentral()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:6.0.4"
    }
}

version projectVersion
group projectGroup

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        mavenCentral()
    }
}

def pdep(String depstr) {
    return (depstr.startsWith(":") ? project(depstr) : depstr)
}
